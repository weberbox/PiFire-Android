plugins {
    id 'com.android.application'
    id 'com.google.gms.google-services'
    id "io.sentry.android.gradle" version "4.0.0"
    id "org.jetbrains.kotlin.android" version "1.9.21"
}

def currentTime = System.currentTimeMillis().toString()

def getGitRevCount = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', '--count', 'HEAD'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    catch (ignored) {
        return 0.toString()
    }
}

def getGitBranch = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', '--abbrev-ref', 'HEAD'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    catch (ignored) {
        return 'Unknown'
    }
}

def vMajor = 2
def vMinor = 1
def vPatch = 3

android {
    compileSdk 34

    defaultConfig {
        applicationId "com.weberbox.pifire"
        minSdk 24
        targetSdk 34
        versionCode vMajor * 1000000 + vMinor * 10000 + vPatch * 100
        versionName "${vMajor}.${vMinor}.${vPatch}"

    }

    buildTypes {
        release {
            manifestPlaceholders = [appName: "PiFire", environment: "production"]
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "String", "BUILD_TIME", "\"${currentTime}\""
            buildConfigField "String", "GIT_BRANCH", "\"${getGitBranch}\""
            buildConfigField "String", "GIT_REV", "\"${getGitRevCount}\""
        }
        debug {
            manifestPlaceholders = [appName: "PiFire Debug", environment: "debug"]
            applicationIdSuffix ".debug"
            debuggable true
            buildConfigField "String", "BUILD_TIME", "\"${currentTime}\""
            buildConfigField "String", "GIT_BRANCH", "\"${getGitBranch}\""
            buildConfigField "String", "GIT_REV", "\"${getGitRevCount}\""
        }
    }

    flavorDimensions += "version"
    productFlavors {
        dev {
            dimension "version"
        }
        github {
            dimension "version"
        }
        playstore {
            dimension "version"
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    buildFeatures {
        viewBinding true
        dataBinding true
    }
    namespace 'com.weberbox.pifire'

    applicationVariants.configureEach { variant ->
        def appName
        if (project.hasProperty("applicationName")) {
            appName = applicationName
        } else {
            appName = parent.name.toLowerCase()
        }
        def dimension = variant.productFlavors[0].name
        def buildType = variant.buildType.name
        def version = variant.versionName

        variant.outputs.configureEach { output ->
            outputFileName = "${appName}-${dimension}-${buildType}${version}.apk"
        }
    }

    if(project.hasProperty("pifire.signing")
            && new File(project.property("pifire.signing") + ".gradle").exists()) {
        apply from: project.property("pifire.signing") + ".gradle"
    }

    if(project.hasProperty("pifire.signing.linux")
            && new File(project.property("pifire.signing.linux") + ".gradle").exists()) {
        apply from: project.property("pifire.signing.linux") + ".gradle"
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'androidx.core:core-splashscreen:1.0.1'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.7.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.7.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.7.7'
    implementation 'androidx.navigation:navigation-ui-ktx:2.7.7'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'androidx.viewpager2:viewpager2:1.1.0-rc01'
    implementation 'androidx.security:security-crypto:1.0.0'
    implementation 'com.pixplicity.easyprefs:EasyPrefs:1.10.0'
    implementation 'com.journeyapps:zxing-android-embedded:4.2.0'
    implementation 'nl.joery.animatedbottombar:library:1.1.0'
    implementation 'com.quiph.ui:recyclerviewfastscroller:1.0.0'
    implementation 'io.sentry:sentry-android:7.1.0'
    implementation 'io.sentry:sentry-android-fragment:7.1.0'
    implementation 'io.sentry:sentry-android-timber:7.1.0'
    implementation 'com.jakewharton.timber:timber:5.0.1'
    implementation 'com.github.discord:OverlappingPanels:0.1.5'
    implementation 'io.github.ParkSangGwon:tedpermission-normal:3.3.0'
    implementation 'com.github.skydoves:androidveil:1.1.2'
    implementation 'com.github.tapadoo:alerter:7.2.4'
    implementation 'me.zhanghai.android.materialratingbar:library:1.4.0'
    implementation 'com.github.acefalobi:android-stepper:0.2.2'
    implementation 'com.airbnb.android:lottie:4.2.1'
    implementation 'com.squareup.okhttp3:okhttp:4.9.3'
    implementation 'com.github.weberbox:changeloglib:3.1.1'
    implementation 'net.yslibrary.keyboardvisibilityevent:keyboardvisibilityevent:3.0.0-RC3'
    implementation 'com.intuit.sdp:sdp-android:1.1.0'
    implementation 'com.intuit.ssp:ssp-android:1.1.0'
    implementation platform('com.google.firebase:firebase-bom:32.8.1')
    implementation 'com.google.firebase:firebase-messaging'
    implementation 'com.onesignal:OneSignal:4.8.7'
    implementation 'com.google.android.play:app-update:2.1.0'
    implementation 'com.google.firebase:firebase-config'
    implementation 'com.google.firebase:firebase-analytics'
    implementation ('io.socket:socket.io-client:2.0.1') {
        exclude group: 'org.json', module: 'json'
    }
    //noinspection GradleDependency
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

}